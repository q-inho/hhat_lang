program                     = imports* (type_file*) / (fns* main?) EOF

imports                     = ( 'use' '(' (typeimport / fnimport)+ ')' )*
typeimport                  = 'type' ':' ( single_import / many_import )
fnimport                    = 'fn' ':' ( single_import / many_import )
single_import               = composite_id_with_closure / id
many_import                 = '[' single_import+ ']'

type_file                   = 'type' ( typesingle / typestruct / typeenum / typeunion )
typesingle                  = simple_id ':' id_composite_value
typemember                  = simple_id ':' id_composite_value
typestruct                  = simple_id '{' typemember* '}'
typeenum                    = simple_id '{' enummember* '}'
typeunion                   = simple_id 'union' '{' unionmember* '}'
enummember                  = simple_id / typestruct
unionmember                 = typemember / typestruct / typeenum
type_trait                  = 'trait' id '{' fns* '}'
typespace                   = 'typespace' ( trait_id / id ) '{' fns* '}'

fns                         = 'fn' simple_id fnargs id? fn_body
fnargs                      = '(' argtype* ')'
argtype                     = simple_id ':' id_composite_value
fn_body                     = '{' (declare / assign / declareassign / return / expr)* '}'
return                      = "::" expr
id_composite_value          = ( '[' id ']' ) / id

main                        = 'main' body

body                        = '{' (declare / assign / declareassign / expr)* '}'
expr                        = cast / callwithargsoptions / callwithbodyoptions / callwithbody / call / array / id / literal
declare                     = simple_id modifier? ':' id
assign                      = id '=' expr
declareassign               = simple_id modifier? ':' id '=' expr
cast                        = ( call / literal / id ) '*' id
call                        = (trait_id '.')? id '(' args* ')' modifier?
args                        = callargs / call / valonly
callargs                    = simple_id ':' valonly
valonly                     = array / literal / id
option                      = (( call / array / id ) ':' body)
callwithbodyoptions         = id '(' args* ')' '{' option+ '}'
callwithargsoptions         = id '(' option+ ')'
callwithbody                = id '(' args* ')' body

array                       = '[' ( literal / composite_id_with_closure / id )* ']'

simple_id                   = r'@?[a-zA-Z][a-zA-Z0-9\-_]*'
composite_id                = simple_id ('.' simple_id)+
composite_id_with_closure   = ( composite_id / simple_id ) '.' '{' ( composite_id_with_closure / composite_id / simple_id )+ '}'
modifier                    = '<' ( valonly+ / callargs+ ) '>'
trait_id                    = simple_id '#' id
id                          = ( composite_id / simple_id ) modifier?

literal                     = null / bool / str / int / float / imag / complex / q__bool / q__int
null                        = 'null'
bool                        = 'true' / 'false'
str                         = r'"([^"]*)"'
int                         = r'-?([1-9]\d*|0)'
float                       = r'0(\.\d+)?|[1-9]\d*(\.\d+)?'
imag                        = (r'0(\.\d+)?|[1-9]\d*(\.\d+)?j') / (r'-?([1-9]\d*|0)j')
complex                     = '[' ( int / float ) imag ']'
q__bool                     = '@true' / '@false'
q__int                      = r'-?\@([1-9]\d*|0)'

comment                     = ( r'\/\/([^\n]*)\n' ) / ( r'\/\-.*?\-\/' )
